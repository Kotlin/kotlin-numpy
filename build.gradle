import org.gradle.internal.jvm.Jvm

/*
 * Kotlin library
 */

plugins {
    // Apply the Kotlin JVM plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'

    // Apply the C plugin to add support for JNI.
    id 'c'

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    // Fat jar
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    id 'maven-publish'

    id "com.jfrog.bintray" version "1.8.4"
}

group 'org.jetbrains'
version '0.1.0'

repositories {
    jcenter()
}

dependencies {
    // Use the Kotlin JDK 8 standard library.
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Use the Kotlin test library.
    testImplementation "org.jetbrains.kotlin:kotlin-test"

    // Use the Kotlin JUnit integration.
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


model {

    buildTypes {
        debug
        release
    }

    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    toolChains {
        gcc(Gcc)
        clang(Clang)
    }

    components {

        def process = new ProcessBuilder("python", "get_args_for_gradle.py")
                .directory(file('buildScr/python'))
                .start()

        def errStream = process.errorStream.readLines()
        if (!errStream.empty) {
            throw new Exception(errStream.join("\n"))
        }

        def paths = process.inputStream.readLines()
        ext.includePythonDir = paths[0]
        ext.includeNumpyDir = paths[1]
        ext.pathLibPython = paths[2]
        ext.libPython = paths[3]

        ktnumpy(NativeLibrarySpec) {

            targetPlatform("x64")

            sources {
                c {
                    source {
                        srcDir "src/main/ktnumpy/jni/source"
                        include "*.c", "**/*.c"
                    }
                    exportedHeaders {
                        srcDirs "src/main/ktnumpy/jni/include"
                    }
                }
            }


            binaries.all {
                if (buildType == buildTypes.debug) {
                    cCompiler.args '-g'
                }
                cCompiler.args '-O3'
                cCompiler.args '-std=gnu99'
                cCompiler.args '-I', "${Jvm.current().javaHome}/include"

                // Macos
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-fPIC'
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
                    cCompiler.args '-I', ext.includePythonDir
                    cCompiler.args '-I', ext.includeNumpyDir
                    linker.args '-L', ext.pathLibPython
                } else if (targetPlatform.operatingSystem.linux) { // linux
                    cCompiler.args '-fPIC'
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cCompiler.args '-I', ext.includePythonDir
                    cCompiler.args '-I', ext.includeNumpyDir
                    linker.args '-L', ext.pathLibPython
                } else { // win
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/win32"
                    cCompiler.args '-I', ext.includePythonDir
                    cCompiler.args '-I', ext.includeNumpyDir
                    linker.args '-L', ext.pathLibPython
                }
                linker.args '-l', ext.libPython
            }
        }
    }
}


jar {
    dependsOn shadowJar
    enabled = false
}

shadowJar {
    classifier = null

    configurations = [project.configurations.compile]

    metaInf {
        from "$buildDir/libs/ktnumpy/shared/release/" into 'Native'
    }

    metaInf {
        from 'buildScr/python' into 'pythonScript' include('*.py')
    }
}

test {
    systemProperty "java.library.path", file("${buildDir}/libs/ktnumpy/shared/release").absolutePath
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenProject(MavenPublication) {
            from components.java
            groupId project.group
            artifactId "kotlin-numpy"
            version project.version

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenProject']
    pkg {
        repo = 'kotlin-numpy'
        name = 'kotlin-numpy'
        userOrg = 'kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Kotlin/kotlin-numpy'
        websiteUrl = 'https://github.com/Kotlin/kotlin-numpy'
        issueTrackerUrl = 'https://github.com/Kotlin/kotlin-numpy/issues'

        githubRepo = 'Kotlin/kotlin-numpy'
        version {
            name = project.version
        }
    }
}
